# Maintainer: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>
# Contributor: Alan Orth <alan.orth[at]gmail[dot]com >
# Submitter: Fredrik Tegenfeldt <fredrik.tegenfeldt@unige.ch>

pkgname=slurm-llnl
pkgver=14.11.4
pkgrel=2
pkgdesc="Simple Linux Utility for Resource Management"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.schedmd.com"
depends=('munge')
makedepends=('python')
optdepends=("hwloc: enables the task/cgroup plugin"
            "lua: the lua API will be available in various plugins"
            "libmariadbclient: support for accounting"
            "postgresql-libs: support for PostgreSQL"
            "hdf5: support for acct_gather_profile/hdf5 job profiling"
            "blcr: support for the checkpoint/blcr plugin"
            "openssl: support for the crypto/openssl CryptoType plugin"
            "pam: PAM support"
            "readline: Readline support in scontrol and sacctmgr's interactive modes"
            "libibmad: support for the acct_gather_infiniband/ofed InfiniBand accounting plugin"
            "libibumad: support for the acct_gather_infiniband/ofed InfiniBand accounting plugin"
            "man2html: HTML versions of the man pages will be generated"
            "rrdtool: support for the ext_sensors/rrd plugin"
            "numactl: NUMA support in the task/affinity plugin"
            "ncurses: adds the smap command "
            "gtk2: enables the sview command, a simple graphical frontend")
install=${pkgname}.install
source=("slurm-tmpfiles.conf"
        "slurm-llnl-default-conf"
        "slurmctld.service.patch"
        "slurmd.service.patch"
        "slurmdbd.service.patch"
        "http://www.schedmd.com/download/total/slurm-${pkgver}.tar.bz2")
md5sums=('51e4ae2c51edf7c145d1b87bec4c344e'
         'cae664b8ef44d01783dade9088e25b53'
         'c4cda06b6fbfb7bd0b46e38d7ce3d038'
         '9318b1a6741c3d9afe3901e6fcf75908'
         '2208258cc5fe877b9f434e9c513c4c4f'
         '6b7dc7e96fffa1f21bc68aac4949eb54')


build() {
  cd "$srcdir/slurm-${pkgver}${_subver}"

  # If required libraries or header files are in non-standard locations,
  # set CFLAGS and LDFLAGS environment variables accordingly.
  # Optional Slurm plugins will be built automatically when the configure script
  # detects that the required build requirements are present.
  #
  # Read http://slurm.schedmd.com/quickstart_admin.html for more information

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin  \
    --sysconfdir=/etc/slurm-llnl  \
    --localstatedir=/var

  # The service files are created in the configure step, therefore the patching is done afterwards
  patch -p0 -i ../slurmctld.service.patch
  patch -p0 -i ../slurmd.service.patch
  patch -p0 -i ../slurmdbd.service.patch

  make
}

package() {
  cd "$srcdir/slurm-${pkgver}${_subver}"

  make DESTDIR="$pkgdir/" install

  # slurm configs and license
  install -D -m644 etc/slurm.conf.example    "${pkgdir}/etc/slurm-llnl/slurm.conf.example"
  install -D -m644 etc/slurmdbd.conf.example "${pkgdir}/etc/slurm-llnl/slurmdbd.conf.example"
  install -D -m644 LICENSE.OpenSSL           "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.OpenSSL"
  install -D -m644 COPYING                   "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # init related install
  install -D -m755 etc/init.d.slurm    "${pkgdir}/etc/rc.d/slurm"
  install -D -m755 etc/init.d.slurmdbd "${pkgdir}/etc/rc.d/slurmdbd"

  # SystemD related install
  install -D -m644 etc/slurmd.service     "${pkgdir}/usr/lib/systemd/system/slurmd.service"
  install -D -m644 etc/slurmctld.service  "${pkgdir}/usr/lib/systemd/system/slurmctld.service"
  install -D -m644 etc/slurmdbd.service   "${pkgdir}/usr/lib/systemd/system/slurmdbd.service"
  install -D -m644 ../slurm-tmpfiles.conf "${pkgdir}/etc/tmpfiles.d/slurm-tmpfiles.conf"

  install -D -m644 ../slurm-llnl-default-conf "${pkgdir}/etc/default/slurm-llnl"

  # creating a log dir
  install -d -m755 "${pkgdir}/var/log/slurm-llnl"
}
