# Maintainer: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>
# Contributor: Alan Orth <alan.orth[at]gmail[dot]com >
# Submitter: Fredrik Tegenfeldt <fredrik.tegenfeldt@unige.ch>

pkgname=slurm-llnl
pkgver=14.11.4
pkgrel=1
pkgdesc="Simple Linux Utility for Resource Management"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.schedmd.com"
# python2 is not required by slurm itself but is used in a patch file - TODO fix later
depends=('python2' 'munge')
optdepends=("hwloc: enables the task/cgroup plugin"
            "lua: the lua API will be available in various plugins"
            "libmariadbclient: support for accounting"
            "postgresql-libs: support for PostgreSQL"
            "hdf5: support for acct_gather_profile/hdf5 job profiling"
            "blcr: support for the checkpoint/blcr plugin"
            "openssl: support for the crypto/openssl CryptoType plugin"
            "pam: PAM support"
            "readline: Readline support in scontrol and sacctmgr's interactive modes"
            "libibmad: support for the acct_gather_infiniband/ofed InfiniBand accounting plugin"
            "libibumad: support for the acct_gather_infiniband/ofed InfiniBand accounting plugin"
            "man2html: HTML versions of the man pages will be generated"
            "rrdtool: support for the ext_sensors/rrd plugin"
            "numactl: NUMA support in the task/affinity plugin"
            "ncurses: adds the smap command "
            "gtk2: enables the sview command, a simple graphical frontend")
provides=('slurm')
install=${pkgname}.install
source=("slurm-tmpfiles.conf"
        "slurmdbd.service"
        "slurmd.service"
        "slurmctld.service"
        "arch_rc.d.slurm"
        "arch_rc.d.slurmdbd"
        "shtml2html.py.patch"
        "man2html.py.patch"
        "http://www.schedmd.com/download/total/slurm-${pkgver}.tar.bz2")

md5sums=('51e4ae2c51edf7c145d1b87bec4c344e'
         '4d25602163a79a57a6c5c9ca613552ff'
         'a4c5c195e0fa149a48f1e0b247a58692'
         '24dfae3cb5538b0e382eee04a392f12b'
         '677f66c6f33538daa46bae831c973763'
         'e439cccf98d45c5551aa326984ba6e9b'
         '9d8f907b340cedc28f6148fb8665f0c2'
         'ae350b014821412ba06b936d673517c9'
         '6b7dc7e96fffa1f21bc68aac4949eb54')


build() {
  cd "$srcdir/slurm-${pkgver}${_subver}"

  patch -p0 -i ../shtml2html.py.patch

  # If required libraries or header files are in non-standard locations,
  # set CFLAGS and LDFLAGS environment variables accordingly.
  # Optional Slurm plugins will be built automatically when the configure script
  # detects that the required build requirements are present.
  # Read http://slurm.schedmd.com/quickstart_admin.html for more information

  ./configure --prefix=/usr           \
  --sbindir=/usr/bin      \
  --sysconfdir=/etc/slurm \
  --localstatedir=/       \

  make
}

package() {
  cd "$srcdir/slurm-${pkgver}${_subver}"

  make DESTDIR="$pkgdir/" install

  # slurm configs and license
  install -D -m644 etc/slurm.conf.example    "${pkgdir}/etc/slurm/slurm.conf.example"
  install -D -m644 etc/slurmdbd.conf.example "${pkgdir}/etc/slurm/slurmdbd.conf.example"
  install -D -m644 LICENSE.OpenSSL           "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.OpenSSL"
  install -D -m644 COPYING                   "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # init related install
  install -D -m755 ../arch_rc.d.slurm    "${pkgdir}/etc/rc.d/slurm"
  install -D -m755 ../arch_rc.d.slurmdbd "${pkgdir}/etc/rc.d/slurmdbd"

  # SystemD related install
  install -D -m644 ../slurmd.service        "${pkgdir}/usr/lib/systemd/system/slurmd.service"
  install -D -m644 ../slurmctld.service     "${pkgdir}/usr/lib/systemd/system/slurmctld.service"
  install -D -m644 ../slurmdbd.service      "${pkgdir}/usr/lib/systemd/system/slurmdbd.service"
  install -D -m644 ../slurm-tmpfiles.conf   "${pkgdir}/etc/tmpfiles.d/slurm-tmpfiles.conf"
}
